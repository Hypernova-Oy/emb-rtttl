#!/usr/bin/perl

# Copyright 2020 Hypernova Oy
# Copyright 2016 Vaara-kirjastot
#

use Modern::Perl;
use Carp;
use Getopt::Long qw(:config no_ignore_case);

use RTTTL::Daemon;
use RTTTL::Player;

my $help;
my $verbose = 0;
my $operation = '';
my $rtttl;
my $daemon;
my %params = ();

GetOptions(
    'h|help'                      => \$help,
    'v|verbose:i'                 => \$params{cverbose},
    'p|pin:i'                     => \$params{pin},
    'd|dir:s'                     => \$params{dir},
    'o|operation:s'               => \$operation,
    'r|rtttl:s'                   => \$rtttl,
    'daemon'                      => \$daemon,
    'rundir:s'                    => \$params{rundir},
    'user:s'                      => \$params{user},
    'group:s'                     => \$params{group},
);

my $usage = <<USAGE;


A happy RTTTL-player to play RTTTL-songs from the given directory
You can configure some of these options in /etc/emb-rtttl/config


  -v --verbose            Defaults to 0, minimal output.
                          1, some output

  -h --help               HELP!

  -o --operation
                  list    list all songs in the directory
                  song    play the target song, or all songs matching the given regexp
                  random  play a random song
                  rtttl   Same as -r --rtttl

  -d --dir                Which directory to use as the song base?

  -p --pin                Which GPIO pin to use for playback?

  -r --rtttl              rtttl-song/codes to play, eg.
                          ToveriAccessDenied:d=4,o=4,b=100:32e,32d,32e,4c

  --daemon                Starts a daemon process listening at /run/emb-rtttl/sock
  --rundir PATH           Where the daemon socket and pid are placed, defaults to ./

  --user NAME             Defaults to pi
  --group NAME            Defaults to gpio

EXAMPLES:

  perl scripts/rtttl-player.pl -p 1 -d rtttl -o list
  perl scripts/rtttl-player.pl -p 1 -d rtttl -o song-nyancat
  rtttl-player.pl -p 1 -o random
  rtttl-player --daemon

USAGE

if ($help) {
  print $usage;
  exit 0;
}
unless (defined($daemon) || $rtttl || $operation =~ /(^list$)|(^song-\w+)|(^random$)|(^rtttl$)/) {
  print $usage."\n".
        "You must define the --operation or --rtttl. Your --operation is '$operation'\n";
  exit 1;
}


my $player = RTTTL::Player->new(\%params);

if ($rtttl) {
  $player->_playSong($rtttl);
}
elsif ($operation) {
  $player->dispatchOperation($operation);
}
elsif (defined($daemon)) {
  RTTTL::Daemon::start_daemon($player);
}
else {
  die "I dont know what to do";
}
